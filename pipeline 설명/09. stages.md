# 09. stages [(공식문서를 보려면 여기로)](https://www.jenkins.io/doc/book/pipeline/syntax/#stages)
## 목적
다른 태그들은 모두 편의를 위한 옵션에 불과하며 실질적인 기능을 수행하는 모든 구문은 여기서 작성한다.

## stage
stages는 필수 태그이며 하위태그로 1개 이상의 stage("")를 사용한다. 괄호 내부에는 내가 구별하기 위한 명칭을 대입하면 된다.<br>해당 명칭은 빌드할 때 다음과 같이 표시되며, 각 stage마다 로그를 확인할 수 있다.
![stage](/images/stage.png)

## stage 하위 태그
stage 하위 태그로는 다음의 4가지 중 1가지를 사용하여야 한다.<br>순차실행(steps), 순차실행 세분화(stages), 병렬 동시 실행(parallel), 경우의 수별로 순차 또는 병렬실행(matrix)을 하나씩 살펴보겠다.

### 1. steps
steps 블럭 내부에 실제로 수행할 내용을 한줄씩 입력하면 된다. 순차적으로 실행된다.
```groovy
pipeline {
    agent any
    environment { //전역변수 저장
        SERVER_CREDENTIALS = credentials('credentials 식별자')
    }
    stages { // jenkins 컨테이너일 때)내부의 /var/jenkins_home/workspace/{myJobName}에서 실행된다
        stage("git clone") {
            steps { // git은 steps 내부 함수다. 현재 위치에 .git x -> git clone, .git o -> fetch만 수행
                git url: "GitURL", // 해당 옵션만 줘도 작동한다. 해당 url에서 git clone을 수행한다
                    credentialsId: "credentials 식별자", //public repository면 해당 로그인 정보가 필요없다
                    branch: "main" //해당 옵션을 안 줄 경우 master로 수행한다
            }
        }
        stage('docker push') {
            steps {
                sh 'docker login -u $SERVER_CREDENTIALS_USR -p $SERVER_CREDENTIALS_PSW HarborURL'
                sh "docker build -t imgName ."
                sh "docker push HarborURL" //여기서 harbor url = imgName의 형식으로 맞춰둔다
            }
        }
    }
}
```
  
script : scripted pipeline block 을 가져와서 groovy 를 사용가능. if - else, try - catch 도 사용가능하다.
```groovy
def gv;

pipeline {
    agent any

    stages {
        stage {
            steps {
                script {
                    def browsers = ['chrome', 'firefox']
                    for (int i = 0; i < browsers.size(); ++i) {
                        echo "Testing the ${browsers[i]} browser"
                    }
                    gv = load "script.groovy" //외부 모듈을 불러올 수 있다. pipeline 위에 먼저 선언해야한다.
                    gv.buildApp()
                    gv.testApp()
                }
            }
        }
    }
}
```
위에서 정의한 외부모듈이 될 파일  
파일명 : script.groovy  
```groovy
def buildApp() {
    echo "building the application…"
}

def testApp() {
    echo "testing the application… ${params.Version}" // 다음과 같은 변수도 사용 가능하다
}

return this
```

### 2. stages
stage가 더 작은 stage들로 나눠지게 해준다. 순회적으로 사용이 가능하다.
```groovy
pipeline {
    agent none
    stages {
        stage('Non-Sequential Stage') {
            agent {
                label 'for-non-sequential'
            }
            steps {
                echo "On Non-Sequential Stage"
            }
        }
        stage('Sequential') {
            agent {
                label 'for-sequential'
            }
            environment {
                FOR_SEQUENTIAL = "some-value"
            }
            stages {
                stage('In Sequential 1') {
                    steps {
                        echo "In Sequential 1"
                    }
                }
                stage('In Sequential 2') {
                    steps {
                        echo "In Sequential 2"
                    }
                }
                stage('Parallel In Sequential') {
                    parallel {
                        stage('In Parallel 1') {
                            steps {
                                echo "In Parallel 1"
                            }
                        }
                        stage('In Parallel 2') {
                            steps {
                                echo "In Parallel 2"
                            }
                        }
                    }
                }
            }
        }
    }
}
```

### 3. parallel
블럭 안에 stage 블럭들을 넣으면 병렬 수행이 가능하다.
```groovy
pipeline {
    agent any
    stages {
        stage('Non-Parallel Stage') {
            steps {
                echo 'This stage will be executed first.'
            }
        }
        stage('Parallel Stage') {
            when {
                branch 'master'
            }
            failFast true
            parallel {
                stage('Branch A') {
                    agent {
                        label "for-branch-a"
                    }
                    steps {
                        echo "On Branch A"
                    }
                }
                stage('Branch B') {
                    agent {
                        label "for-branch-b"
                    }
                    steps {
                        echo "On Branch B"
                    }
                }
                stage('Branch C') {
                    agent {
                        label "for-branch-c"
                    }
                    stages {
                        stage('Nested 1') {
                            steps {
                                echo "In stage Nested 1 within Branch C"
                            }
                        }
                        stage('Nested 2') {
                            steps {
                                echo "In stage Nested 2 within Branch C"
                            }
                        }
                    }
                }
            }
        }
    }
}
```

### 4. matrix
3가지 경우의 수 * 3가지 경우의 수를 배치하면 9가지 경우의 수를 모두 수행해준다. 예시를 보면 쉽게 이해가 간다.
```groovy
matrix {
    axes { // 1개 이상의 axis로 구성된다
        axis { // axis는 각각 이름과 리스트가 필요하다, 다른 axis 에 속한 value와 조합된다
            name 'PLATFORM'
            values 'linux', 'mac', 'windows'
        }
        axis {
            name 'BROWSER'
            values 'chrome', 'edge', 'firefox', 'safari'
        }
        axis {
            name 'ARCHITECTURE'
            values '32-bit', '64-bit'
        }
    }
    excludes { // 제외할 셀 선택
        exclude {
            axis {
                name 'PLATFORM'
                values 'mac'
            }
            axis {
                name 'ARCHITECTURE'
                values '32-bit'
            }
        }
    }
    stages { // 24개의 셀이 각각 3개의 단계를 수행한다.
        stage('build') {
            // ...
        }
        stage('test') {
            // ...
        }
        stage('deploy') {
            // ...
        }
    }
}
```

---
## stage 하위 태그-2
stage 하위 태그이지만 기능을 수행하는 부분이 아닌 태그

### 1. when [공식문서를 보려면 여기로](https://www.jenkins.io/doc/book/pipeline/syntax/#when)
when은 stage의 하위 태그로 조건검사를 통해 해당 stage 내부의 기능을 담당하는 steps, stages, parallel, matrix를 수행할지 결정할 수 있다.<br>여기서는 when의 하위태그 중 일부만을 소개한다. 더 필요한 사항은 공식문서를 참고하자
#### 사용 옵션
environment : 해당 이름의 environment 값이 value와 같을 때 조건검사를 통과한다.
```groovy
pipeline {
    agent any
    environment {
        DEPLOY_TO = "production"
    }
    stages {
        stage {
            when {
                environment name: 'DEPLOY_TO', value: 'production'
            }
            steps {
                sh "echo hi"
                junit 'reports/**/*.xml'  // junit 플러그인을 통해 제공하는 기능으로, 테스트 보고서를 집계한다.
            }
        }
    }
}
```
expression : if 문과 같다. true, false만 검사한다.  (when 하위 태그)
```groovy
pipeline {
    agent any

    stages {
        stage {
            when { 
                expression {
                    // 파이프라인 테스트 실패시 해당 값은 UNSTABLE 이다.
                    currentBuild.result == null || currentBuild.result == 'SUCCESS' 
                }
            }
            steps {
                sh "echo hi"
                junit 'reports/**/*.xml'  // junit 플러그인을 통해 제공하는 기능으로, 테스트 보고서를 집계한다.
            }
        }
    }
}
```
다음 조건에서 실행 allOf: all true, not: all false, anyOf: 1개이상 true  (when 하위 태그)
```groovy
pipeline {
    agent any
    stages {
        stage('Example Build') {
            steps {
                echo 'Hello World'
            }
        }
        stage('Example Deploy') {
            when {
                allOf { //다음 조건에서 실행 allOf: all true, not: all false, anyOf: 1개이상 true
                    branch 'production'
                    environment name: 'DEPLOY_TO', value: 'production'
                }
            }
            steps {
                echo 'Deploying'
            }
        }
    }
}
```

### 2. input
빌드 실행도중 값을 입력받는다. 아직은 필요성이 없어 생략한다. 사용형태만 알아보자
```groovy
pipeline {
    agent any
    stages {
        stage('Example') {
            input {
                message "Should we continue?"
                ok "Yes, we should."
                submitter "alice,bob"
                parameters {
                    string(name: 'PERSON', defaultValue: 'Mr Jenkins', description: 'Who should I say hello to?')
                }
            }
            steps {
                echo "Hello, ${PERSON}, nice to meet you."
            }
        }
    }
}
```

### 3. environment : 전과 같다
### 4. options : 전과 같다 (가능한 옵션 : skipDefaultCheckout, timeout, retry, timestamps)

### 다음 > [10. post](10.%20post.md)
